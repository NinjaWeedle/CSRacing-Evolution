:DCS
"6D9F894A8D8A854A6D5F000000001FC022204210FFFEA229C2279229BBDD1008
Full
BackgroundOn 12
BorderColor 4
DetectAsymOff
AxesOff
ZStandard
84->Xmin
72->Ymax
ZInteger
14
TextColor(Ans
For(A,~40,50,10
	If A=50
	ClrDraw
	Line(A-49,15,A-49,10,11
	Line(A-49,10,A-41,5,Ans
	Line(A-49,15,A-25,19,Ans
	Line(A-25,13,A-25,19,Ans
	Line(A-25,19,A-13,19,Ans
	Line(A-35,14.5,A-25,13,Ans
	Line(A-35,14.5,A-49,Ans,Ans
	Line(A-13,19,A+2,13,Ans
	Line(A+2,13,A+8,12,Ans
	Line(A+8,12,A+20,7,Ans
	Line(A+20,7,A+18,4,Ans
	Line(A+18,4,A+11,4,Ans
	Line(A+11,11,A+15,Ans,Ans
	Line(A+15,Ans,A+15,10,19
	Line(A,4,A-29,4,Ans
End
For(X,0,250,2
	Text(~1,0,X," 
End
Text(0,30,"CALCULATOR STREET RACING II:
12
Line(51,13,25,13,Ans
Line(45,6.5,22,6.5,Ans
Line(52,13,45,6,Ans
Line(29,6.5,25,13,Ans
Circle(55,5,4,Ans
Circle(15,5,4,Ans
TextColor(Ans
Text(~1,25,75,"EVOLUTION
BackgroundOn 22
BackgroundOn 20
Text(140,200,"OXITI8
Text(60,80,"PRESS ENTER
"                      ->Str0
Pause 
SetUpEditor CAE,CSE,TTR
27->dim(|LCSE
If not(|LCSE(Ans
Then
	randInt(1,8^5->|LCSE(Ans
	2|E4->|LCSE(1
	15->|LCSE(2
End
25->dim(|LCAE
6->dim(|LTTR
If not(|LTTR(1
Fill(255,|LTTR
|LCAE(1->A
If not(A
Then
	12->|LCAE(16
	15->|LCAE(1
	"NONE->Str9
End
If not(|LCSE(3
1->|LCSE(3
Lbl 0
BackgroundOff
Full
TextColor(19
BorderColor 1
ClrHome
ClrDraw
DelVar KZDecimal
Text(~1,44,96,Str0
TextColor(12
Text(45,140,"CAR INFO
Text(0,220,"HOME
Horizontal 3.2,24
Text(65,150,"TIER
Text(65,185,|LCAE(17
TextColor(|LCAE(16
Text(23,100,Str9
Line(~1.8,1.1,7,1.1,24
Line(~1.8,1.9,7,1.9,24
TextColor(12
Text(90,100,"HP:
Text(90,165,|LCAE(18
Text(110,100,"LB-FT:
Text(110,165,|LCAE(19
Text(130,100,"WT(KG):
Text(130,165,|LCAE(24
Text(150,100,"REGION:
Text(150,165,|LCAE(22
DelVar CText(0,0,"CR:
Text(0,20,|LCSE(1
17
Text(125,Ans,"BUY CARS
Vertical ~1.8,22
Text(25,Ans,"GO RACE
Text(45,Ans,"TUNING
Text(65,Ans,"PAINTSHOP
Text(85,Ans,"CHANGE CAR
Text(105,Ans,"RECORDS
DelVar BText(145,Ans,"QUIT
Repeat B
	If K
	Text(~1,20C+23,4," 
	C-(C and K=25->C
	C+(K=34 and C<6->C
	Text(20C+25,4,"[|>]
	getKey->K
	K=21 or K=105->B
End
DelVar ADelVar FIf C=1 and 15!=|LCAE(1
Then
	ClrDraw
	Horizontal 3.2,19
	|LCSE(5+|LCAE(1->A
	Text(0,200,"TUNING
	Text(0,0,"CR:
	Line(~3,3.2,~3,~5,19
	Text(0,25,|LCSE(1
	10fPart(.1int(A->L
	10fPart(.1int(.1A->M
	int(.01A->N
	18
	Text(35,Ans,"ENGINE
	Text(65,Ans,"WEIGHT
	Text(95,Ans,"TIRES
	Text(125,Ans,"BACK
	Text(35,5Ans,L
	Text(65,5Ans,M
	Text(95,5Ans,N
	Repeat B
		If K
		Text(~1,30F+33,1," 
		F-(F and K=25->F
		F+(K=34 and F<3->F
		Text(30F+35,2,"[|>]
		getKey->K
		K=21 or K=105->B
	End
	For(X,20,130,10
		Text(~1,X,0,Str0
	End
	If F!=3
	Then
		If not(F
		L->U
		If F=1
		M->U
		If F=2
		N->U
		Horizontal ~3.2,19
		If U=1
		Text(~1,30,95,"STREET
		If U=2
		Text(~1,30,100,"SPORT
		If U>=3
		Text(~1,30,105,"RACE
		"IMPROVES 
		If F<=1
		Ans+"ACCELERATION 
		If not(F
		Ans+"& TOP SPEED
		If F=2
		Ans+"GRIP
		Text(120,0,Ans
		"INSTALL 
		If U=1 and not(F
		Ans+"HI-FLOW AIR FILTER & EXHAUST
		If U=1 and F=1
		"REMOVE SPARE TIRE & REAR SEATS
		If U=1 and F=2
		Ans+"STREET TIRES
		If U=2 and not(F
		"REPROGRAM THE ECU
		If U=2 and F=1
		"REMOVE A/C & PASSENGER SEAT
		If U=2 and F=2
		"SOFT COMPOUND SPORTS TIRES
		If U>=3 and not(F
		Ans+"HIGH-PERFORMANCE CAMS
		If U>=3 and F=1
		"STRIP OUT INTERIOR TRIM
		If U>=3 and F=2
		Ans+"DRAG SLICKS
		Text(90,0,Ans+".
		2500U|LCAE(17->C
		Text(60,140,C
		Text(60,95,"COST:
		Text(150,5,"BACK
		If U<=3
		Text(150,90,"PURCHASE
		Repeat K>=11 and K<=15
			getKey->K
		End
		If K>=12 and K<=15
		Then
			If U<=3 and U and C<=|LCSE(1
			Then
				|LCSE(1)-C->|LCSE(1
				A+(10^(F->|LCSE(5+|LCAE(1
				TextColor(14
				Text(~1,70,95,"OWNED
				DelVar CText(90,20,"SWITCH CARS TO INSTALL URGRADES
			End
			If C
			Text(~1,80,20,"INSUFFICIENT FUNDS
			Pause 
		End
	End
End
If C=2:Then
	Horiz
	Horizontal 2.6,13
	Text(0,180,"PAINTSHOP
	Text(0,0,"CR:
	|LCSE(1->C
	Text(0,25,C
	Text(20,50,"CURRENT COLOR:
	Text(65,93,"COST: 25
	|LCAE(16
	TextColor(Ans
	Text(20,160,Ans
	If 15=|LCAE(1
	Then
		Pause "NO CAR SELECTED
		1->A
	Else
		Text(40,128,"#
		Pt-On(.2,~.7,Ans
		Pt-On(~.15,~.7,Ans
		Input "INPUT COLOR:",A
		If A>24 or A<10
		12->A
		If C>=25 and A!=|LCAE(16
		Then
			A->|LCAE(16
			sub(A)+int(|LCSE(5+|LCAE(1->|LCSE(5+|LCAE(1
			TextColor(12
			Text(20,50,Str0
			C-25->|LCSE(1
			TextColor(A
			Text(20,85,"COLOR CHANGED
		End
		Text(65,93,"PRESS ENTER
		128->B
		Text(40,B,"#
		Pt-On(.2,~.7,A
		Pt-On(~.15,~.7,A
		Pause 
		For(B,122,250,2
			Text(40,B,"#
		End
	End
	Full
End
If C=4:Then
	20->A
	ClrDraw
	Text(0,195,"RECORDS
	Horizontal 3.2,12
	Text(A,92,"TIME TRIALS
	Horizontal 2.3,12
	Text(40,190,"LONG
	Text(40,80,"SHORT
	Text(60,0,"TIER 1
	Text(80,0,"TIER 2
	Text(A5,0,"TIER 3
	For(F,1,3
		Text(40+AF,85,|LTTR(F
	End
	For(F,1,3
		Text(40+AF,195,|LTTR(3+F
	End
	Line(~4.4,2.2,~4.4,~1.8,12
	TextColor(10
	Text(135,50,"LADDER:
	Text(135,105,|LCSE(2)-14
	Text(135,125,"/30
	TextColor(15
	Text(6A,145,iPart(|LCSE(3)-.9
	Text(6A,50,"CAR SPECIFIC:
	Text(6A,165,"/13
	TextColor(16
	Text(150,50,"SPECIAL EVENTS:
	Text(150,165,|LCSE(4
	Text(150,185,"/6
	Horizontal 1.2,11
	Horizontal .2,14
	Horizontal ~.8,18
	Horizontal ~1.8,12
	Line(1,2.2,1,~1.8,12
	Pause 
End
DelVar FIf C=5:Then
	DelVar EDelVar DDelVar BBorderColor 3
	1->A
	Repeat F=3 or D=3
		ClrDraw
		Horizontal 3.2,17
		Horizontal 2.3,17
		Text(0,190,"BUY CARS
		Text(0,0,"CR:
		|LCSE(1->C
		Text(0,25,C
		Text(20,70,"SELECT CONTINENT
		Text(55,70,"NORTH AMERICA
		Text(80,70,"EUROPE
		Text(105,70,"ASIA
		Text(130,70,"BACK
		Repeat B
			If K
			Text(~1,25F+53,50," 
			F-(F and K=25->F
			F+(K=34 and F<3->F
			Text(25F+55,50,"[|>]
			getKey->K
			K=21 or K=105->B
		End
		For(X,20,140,15
			Text(~1,X,0,Str0
		End
		Horizontal 2.3,17
		TextColor(12
		Text(145,80,"BACK
		If not(F
		Then
			Text(20,80,"NORTH AMERICA
			Text(45,80,"FOCUS RS '09
			Text(70,80,"CAR*0
			Text(95,80,"CAR=0
			Text(120,80,"not(CAR
		End
		If F=1
		Then
			Text(20,90,"EUROPE
			Text(45,80,"COUNTACH LP400 '74
			Text(70,80,"911 GT3 RS '16
		End
		If F=2
		Then
			Text(20,110,"ASIA
			Text(45,80,"CRX SI '91
		End
		If F!=3
		Then
			Repeat E
				If K
				Text(~1,25D+43,60," 
				D-(D and K=25->D
				D+(D<4 and K=34->D
				Text(25D+45,60,"[|>]
				getKey->K
				K=21 or K=105->E
			End
			If D!=4
			Then
				If not(F
				Then
					If not(D
					Then
						"FOCUS RS '09->Str3
						35|E3->C
						3->N
						2->I
						14->B
					End
				End
				If F=1
				Then
					If not(D
					Then
						"COUNTACH LP400 '74->Str3
						11|E4->C
						4->N
						2->I
						15->B
					End
					If D=1
					Then
						"911 GT3 RS '16->Str3
						15|E4->C
						5->N
						3->I
						15->B
					End
				End
				If F=2
				Then
					If not(D
					Then
						"CRX SI '91->Str3
						18|E3->C
						1->N
						1->I
						14->B
					End
				End
				ClrDraw
				|LCSE(N+5->O
				Horizontal 3.2,17
				Text(0,190,"BUY CARS
				Horizontal ~3.2
				Text(0,0,"CR:
				Text(0,25,|LCSE(1
				If not(Z
				B->Z
				TextColor(B
				Text(50,60,Str3
				Text(75,128,"#
				Pt-On(.2,0,B
				Pt-On(~.15,0,B
				TextColor(12
				Text(25,110,"TIER
				Text(25,150,I
				Text(97,84,"COST:
				Text(120,84,"CAR #:
				Text(120,140,N
				Text(97,140,C
				Text(150,5,"BACK
				If O
				Then
					Text(150,95,"OWNED
				Else
					Text(150,95,"PURCHASE CAR
				End
				DelVar KLine(~4.5,~5,~4.5,~3.2
				Repeat K>=11 and K<=15
					getKey->K
				End
				If K>11 and not(O
				Then
					If C<=|LCSE(1
					Then
						|LCSE(1)-C->|LCSE(1
						111+sub(B->|LCSE(5+N
						TextColor(14
						Text(~1,80,50,"CAR PURCHASED
					Else
						TextColor(11
						Text(~1,80,20,"INSUFFICIENT FUNDS
					End
					TextColor(12
					Pause 
				End
			End
		End
	End
End
If A
Goto 0
If C=6:Then
	ClrHome
	Disp "SAVING
	Archive |LCSE
	Archive |LTTR
	ClrDraw
	Return
End
If C=3:Then
	DelVar FDelVar BClrDraw
	Text(0,180,"CHANGE CAR
	Horizontal 3.2
	For(Y,0,1
		For(X,0,5
			Text(20X+25,20+130Y,"???
		End
	End
	If |LCSE(6
	Text(25,20,"CRX Si
	If |LCSE(7
	Text(45,20,"1500 LARAMIE HEMI
	If |LCSE(8
	Text(65,20,"FOCUS RS
	If |LCSE(9
	Text(85,20,"COUNTACH LP400
	If |LCSE(10
	Text(105,20,"911 GT3 RS
	If |LCSE(11
	Text(125,20,"CAR 6
	DelVar FText(145,20,"BACK
	Repeat B
		If K
		Text(~1,20A+23,4+130F," 
		A-(A and K=25->A
		A+(K=34 and A<6->A
		F-(F and K=24->F
		F+(K=26 and not(F->F
		Text(20A+25,4+130F,"[|>]
		getKey->K
		K=21 or K=105->B
	End
	If F
	Then
		
	Else
		If A=4 and |LCSE(10
		Then
			"911 GT3 RS '16->Str9
			{5,8.8,6,193,7,3.75,2.38,1.72,1.34,1.11,.96,.84,3.2,.25,.1,15,3,500,339,28.7,4.19,2,10,1420,1->CAE
		End
		If A=3 and |LCSE(9
		Then
			"COUNTACH LP400 '74->Str9
			{4,8,5,186,5,2.256,1.769,1.31,.99,.775,0,0,2.2,.2,.13,15,2,375,365,25.3,4.091,2,12,1300,1->CAE
		End
		If A=2 and |LCSE(8
		Then
			"FOCUS RS '09->Str9
			{3,7,6.5,156,6,3.23,1.95,1.32,1.03,.821,.685,0,1.9,.25,.15,14,2,300,318,25.5,4.06,1,9,1741,2->CAE
		End
		If A=1 and |LCSE(7
		Then
			"LARAMIE->Str9
			{2->CAE
		End
		If |LCSE(6)not(A
		Then
			"CRX SI '91->Str9
			{1,8.2,6,153,5,3.166,2.052,1.416,1.103,.87,0,0,1.4,.1,.15,11,1,108,100,22.5,4.266,3,8,954,2->CAE
		End
	End
	ClrDraw
	Text(0,180,"CHANGE CAR
	Horizontal 3.2
	Text(75,20,"GOT IN
	Text(110,99,"CAR
	Text(110,130,|LCAE(1
	Text(150,93,"PRESS ENTER
	|LCSE(5+|LCAE(1->U
	If U
	Then
		~.1+fPart(.1int(U->A
		A+|LCAE(13->|LCAE(13
		A10->A
		A2+|LCAE(4->|LCAE(4
		A10+|LCAE(18->|LCAE(18
		A10+|LCAE(19->|LCAE(19
		A+|LCAE(23->|LCAE(23
		~.1+.1remainder(100,int(.1U->A
		A+|LCAE(13->|LCAE(13
		|LCAE(24)-500A->|LCAE(24
		|LCAE(23)-2int(.01U->|LCAE(23
		100fPart(U->|LCAE(16
		TextColor(Ans
	End
	Text(75,65,Str9
	Pause 
	Goto 0
End
Lbl 2
15=|LCAE(1
If Ans
Pause "YOU NEED A CAR TO RACE.
If Ans
Goto 0
"CPU->Str2
1->L
DelVar BDelVar DDelVar JDelVar WDelVar thetaClrDraw
Menu("EVENT LIST","REGULATION",A,"LADDER",B,"TIME TRIALS",C,"CAR SPECIFIC",D,"SPECIAL EVENTS",E,"MACHINE TEST",F,"BACK TO HOME",0
Lbl F
8->L
Goto 1
Lbl E
4->W
BackgroundOn 22
Horizontal 3.2,16
For(X,0,5
	TextColor(12
	If |LCSE(21+X
	TextColor(20
	If not(X
	Text(25,17,"small1. FF CHALLENGE
	If X=1
	Text(45,17,"small2. TIER 1 CHAMPIONSHIP
	If X=2
	Text(65,17,"small3. JP TUNER TRIALS
	If X=3
	Text(85,17,"small4. TIER 2 CHAMPIONSHIP
	If X=4
	Text(105,17,"small5. TOP SPEED SHOWDOWN
	If X=5
	Text(125,17,"small6. TIER 3 CHAMPIONSHIP
End
TextColor(20
Text(0,145,"SPECIAL EVENTS
Text(145,17,"BACK
Repeat B
	If K
	Text(~1,20C+23,4," 
	C-(C and K=25->C
	C+(K=34 and C<6->C
	Text(20C+25,4,"[|>]
	getKey->K
	K=21 or K=105->B
End
|LCAE(22->B
|LCAE(17->A
If C=2
Then
	If B=3 and A=2
	Then
		
	Else
		6->C
	End
End
.1(C+1->theta
1->D
.1->J
If C=6
Goto 0
Goto 1
Lbl D
5->W
|LCSE(3->A
TextColor(15
Horizontal 3
Text(0,75,"CAR SPECIFIC
If A>=31
Then
	TextColor(12
	Text(~1,80,60,"CUSTOM SPEC
	Pause 
	Input "LAUNCH SPD?",D
	Input "ACCEL LV?",J
	Input "LENGTH?",L
	.1D->D
Else
	SetUpEditor L1
	.5->B
	.2->C
	L1(A->D
	L1(6+A->J
	W(A=|LCAE(1->W
	Text(~1,50,80,"CAR
	Text(~1,50,120,A
	If W
	Text(120,40,Str1
	If not(W
	Text(80,80,"REQUIRE\D
	Pause 
End
If not(W
Goto 2
Goto 1
Lbl C
2->W
Text(0,170,"TIME TRIALS
Horizontal 3.2,11
Horizontal 2.3,11
Text(20,99,"TIER
|LCAE(17
DelVar AText(20,134,Ans
Text(50,150,|LTTR(3+Ans
Text(80,150,|LTTR(Ans
Text(50,30,"LONG TRACK
Text(80,30,"SHORT TRACK
Text(110,30,"BACK
Repeat B
	If K
	Text(~1,30A+51,14," 
	A-(A and K=25->A
	A+(K=34 and A<2->A
	Text(30A+50,14,"[|>]
	getKey->K
	Ans=105 or Ans=21->B
End
If not(A
Then
	2->L
End
If A=1
Then
	1->L
End
If A=2
Goto 2
|LCAE(17)+3(L=2->theta
240L/|LTTR(theta->D
"GHOST->Str2
21->Q
Goto 1
Lbl B
3->W
BorderColor 3
Text(0,170,"LADDER RACE
TextColor(18
Horizontal 3.2,18
Text(~1,80,95,|LCSE(2)-14
Text(~1,80,120,"/30
.1|LCSE(2->D
Text(30,50,"CR. PRIZE:
.5->L
Text(30,125,W500D
If 44=|LCSE(2
Text(30,125,1|E4
Text(150,90,"PRESS ENTER
Pause 
Goto 1
Lbl A
1->W
BorderColor 2
Horiz
Text(0,170,"REGULATION
TextColor(14
Horizontal 2.6,14
Input "DIFFICULTY?",D
If D<0
randInt(10,70->D
D.1->D
Input "LENGTH?",L
abs(L->L
If L>5 or not(L
1->L
Text(40,50,"CR. PRIZE:
Text(40,130,W500D/L
Pause "PRESS ENTER
Lbl 1
Full
BackgroundOff
BorderColor 1
DelVar SDelVar RDelVar PDelVar OClrDraw
ZDecimal
0->Xmin
264->Xmax
1->G
Text(0,75,"VS.
Text(0,99,Str2
19
If W>=4
20
TextColor(Ans
Text(~1,51,0,Str0
Text(~1,65,0,Str0
Text(~1,80,0,Str0
Line(0,.3,250,.3,Ans
Line(0,.5,250,.5,Ans
12
Shade(~.8,1.6,250,264,4,2,Ans
If W>=4
Then
	Line(15,.4,35,.4,Ans
	23
End
Horizontal ~.8,Ans
Horizontal 1.65,Ans
|LCAE(16->N
TextColor(N
Text(34,0,"YOU
Text(55,0,"#
Pt-On(1,.95,Ans
Pt-On(8,.95,Ans
If W=3
|LCSE(2->rand
If W!=2
randInt(10,24->Q
TextColor(Q
Text(80,0,"#
Pt-On(1,~.3,Q
Pt-On(8,~.3,Q
Text(99,0,Str2
TextColor(12
Text(150,188,"LENGTH:
Text(150,243,L
Text(0,60,"PRESS 2ND TO REV
Repeat Ans=21
	getKey
End
Text(~1,0,60,Str0
Text(~1,32,0,Str0
Text(~1,99,0,Str0
|LCAE(2->A
|LCAE(3->B
|LCAE(5->C
|LCAE(14->E
|LCAE(15->H
|LCAE(21->M
|LCAE(23->V
Text(106,10,"RPM (X1000)
Text(5,98,"Time
For(Z,3,0,~.05
	TextColor(12
	Text(~1,130,25,R
	If R=round(R,0
	Text(~1,130,37,".0
	Text(~1,20,90,Z
	If W>=4 and not(fPart(Z
	Pt-On(18+4Z,.4,19
	getKey->K
	If R<B
	Then
		TextColor(11
		Text(150,80,"REV MORE
	Else
		If R=B
		Then
			TextColor(14
			Text(150,80,"PERFECT!
		Else
			TextColor(11
			Text(150,80,"REV LESS
		End
	End
	If K and R<A
	Then
		R+(K=21->R
		R+E2not(K=21->R
	Else
		If R>A
		A->R
		abs(R-E->R
	End
	TextColor(12
End
If W>=4
Pt-On(18,.4,14
Text(~1,20,90,Str0
DelVar ZText(~1,130,25,R
|LCAE(13->A
If B=round(R,0
Then
	TextColor(18
	Text(150,80,"GOOD START
	5+|LCSE(1->|LCSE(1
	B.1->S
Else
	TextColor(11
	If R>B
	Then
		V+int(R/2->V
		B.1->S
	Else
		V-1->V
	End
	Text(150,80,"POOR START
	.1->S
End
If R=B
Then
	TextColor(14
	Text(150,80,"PERFECT START
	8+|LCSE(1->|LCSE(1
	.05(R->S
End
|LCAE(6->F
TextColor(12
Text(106,160,"GEAR
Text(106,115,"MPH
Text(~1,130,170,G
Repeat P>=240L
	getKey->K
	If K=25 and G<C
	Then
		G+1->G
		V-1->V
		|LCAE(5+G->F
		Text(~1,130,170,G
	End
	If K=34 and G>1
	Then
		G-1->G
		|LCAE(5+G->F
		Text(~1,130,170,G
	End
	Text(~1,130,115,round(10S,0
	round(10S336FM/|LCAE(20),0)/1|E3->R
	Text(~1,130,25,R
	If S10>|LCAE(4
	.1|LCAE(4->S
	If R>|LCAE(2
	S-(.15A(1.1-GH->S
	TextColor(19
	S+(1.1-GH)(.1(A(1+(V>0).3->S
	If W>3
	D+.1J->D
	Text(~1,78,round(O/L,0)," 
	If O<240L
	Then
		O+D->O
	Else
		~9->D
	End
	If V>0
	Then
		V-1->V
	Else
		Text(~1,53,round(P/L,0)," 
	End
	P+S->P
	TextColor(N
	round(P/L,0
	Text(55,Ans,"#
	Pt-On(Ans+1,.95,N
	Pt-On(Ans+8,.95,N
	TextColor(Q
	round(O/L,0
	Text(80,Ans,"#
	Pt-On(Ans+1,~.3,Q
	Pt-On(Ans+8,~.3,Q
	TextColor(12
	Z+1->Z
	Text(~1,20,98,Z
End
If D=~9
Then
	int(L+|LCSE(1->|LCSE(1
	DelVar thetaText(~1,78,75,"YOU LOSE
	If W=5
	Text(60,30,Str3
	If W=2 and 11<=|LCSE(2
	|LCSE(2)-1->|LCSE(2
Else
	TextColor(14
	Text(~1,53,80,"YOU WIN
	TextColor(12
	If W=5 and 14>|LCSE(3
	1+|LCSE(3->|LCSE(3
	If W=3 and 41>|LCSE(2
	1+|LCSE(2->|LCSE(2
	If 15>|LCSE(4
	(5=W)+|LCSE(4->|LCSE(4
	If W=3 and 44=|LCSE(2
	Then
		5200+|LCSE(1->|LCSE(1
		Text(95,30,"YOU'RE THE NEW LADDER CHAMP!
	Else
		(W=2)+|LCSE(2->|LCSE(2
	End
	W500D
	If L>=1
	Ans/L
	int(Ans+|LCSE(1->|LCSE(1
End
TextColor(12
Text(150,80,"PRESS ENTER            
Text(0,0,"CR:
Text(0,20,|LCSE(1:Pause 
If theta
Then
	If not(fPart(theta
	Then
		If Z<|LTTR(theta
		Then
			Disp "NEW RECORD!
			Z->|LTTR(theta
		Else
			Disp "RECORD TIED
		End
		DelVar thetaDisp "TIME:
		Pause Z
	End
End
If theta
Goto 1
Goto 0