:DCS
"03303303300555553000300303005050300033033000505030000303030050500330330303055555BBBBBBBBBBBBBBBB9999999999999999999999999999999999999999999999999993333339999999993CC3CCC399999953555355553555992555535555355AA9533355355533355533D33333333D33339333999999333999
Full
BackgroundOn 12
BorderColor 4
DetectAsymOff
AxesOff
ZStandard
84->Xmin
72->Ymax
ZInteger
14
TextColor(Ans
For(A,~40,50,10
	If A=50
	ClrDraw
	DelVar HLine(A-49,15,A-49,10,11
	Line(A-49,10,A-41,5,Ans
	Line(A-49,15,A-25,19,Ans
	Line(A-25,13,A-25,19,Ans
	Line(A-25,19,A-13,19,Ans
	Line(A-35,14.5,A-25,13,Ans
	Line(A-35,14.5,A-49,Ans,Ans
	Line(A-13,19,A+2,13,Ans
	Line(A+2,13,A+8,12,Ans
	Line(A+8,12,A+20,7,Ans
	Line(A+20,7,A+18,4,Ans
	Line(A+18,4,A+11,4,Ans
	Line(A+11,11,A+15,Ans,Ans
	Line(A+15,Ans,A+15,10,19
	Line(A,4,A-29,4,Ans
End
For(X,0,250,2
	Text(~1,0,X," 
End
Text(0,30,"CALCULATOR STREET RACING II:
12
Line(51,13,25,13,Ans
Line(45,6.5,22,6.5,Ans
Line(52,13,45,6,Ans
Line(29,6.5,25,13,Ans
Circle(55,5,4,Ans
Circle(15,5,4,Ans
TextColor(Ans
Text(~1,25,75,"EVOLUTION
BackgroundOn 22
BackgroundOn 20
Text(140,200,"OXITI8
Text(60,80,"PRESS ENTER
"                      ->Str0
Pause 
SetUpEditor CAE,CSE,TTR
27->dim(|LCSE
If not(|LCSE(Ans
Then
	randInt(1,8^5->|LCSE(Ans
	2|E4->|LCSE(1
	15->|LCSE(2
End
6->dim(|LTTR
If not(|LTTR(1
Fill(255,|LTTR
If not(|LCSE(3
1->|LCSE(3
Lbl 0
26->dim(|LCAE
If not(|LCAE(1
Then
	12->|LCAE(16
	15->|LCAE(1
	"NONE->Str9
End
BackgroundOff
Full
TextColor(19
BorderColor 1
ClrHome
DelVar KZDecimal
Text(~1,44,96,Str0
TextColor(12
Text(45,145,"CAR INFO
Text(0,220,"HOME
Horizontal 3.2,24
Text(65,155,"TIER
Text(65,190,|LCAE(17
TextColor(|LCAE(16
~1.8
Line(Ans,1.1,7,1.1,24
Line(Ans,1.9,7,1.9,24
Vertical Ans,22
Line(6.6,1.1,6.6,~Ans,22
100
Text(23,Ans,Str9
TextColor(12
Text(90,Ans,"HP:
Text(90,165,|LCAE(18
Text(110,Ans,"LB-FT:
Text(110,165,|LCAE(19
Text(130,Ans,"WT(KG):
Text(130,165,|LCAE(24
Text(150,Ans,"REGION:
|LCAE(22->X
If X=1
"USA
If X=2
"EUR
If X=3
"JPN
Text(150,165,Ans
DelVar CText(0,0,"CR:
Text(0,20,|LCSE(1
17
Text(125,Ans,"BUY CARS
Text(25,Ans,"GO RACE
Text(45,Ans,"TUNING
Text(65,Ans,"PAINTSHOP
Text(85,Ans,"CHANGE CAR
Text(105,Ans,"RECORDS
Text(145,Ans,"QUIT
Repeat K=21 or K=105
	If K
	Text(~1,20C+23,4," 
	C-(C and K=25)+(K=34 and C<6->C
	Text(20C+25,4,"[|>]
	getKey->K
End
DelVar ADelVar FIf C=1 and 15!=|LCAE(1
Then
	ClrDraw
	Horizontal 3.2,19
	|LCSE(5+|LCAE(1->A
	Text(0,200,"TUNING
	Text(0,0,"CR:
	Line(~3,3.2,~3,~5,19
	Text(0,25,|LCSE(1
	10fPart(.1int(A->L
	10fPart(.1int(.1A->M
	int(.01A->N
	18
	Text(25,Ans,"ENGINE
	Text(50,Ans,"WEIGHT
	Text(75,Ans,"TIRES
	Text(100,Ans,"RESET
	Text(125,Ans,"RATIOS
	Text(150,Ans,"BACK
	Text(25,5Ans,L
	Text(50,5Ans,M
	Text(75,5Ans,N
	Repeat K=21 or K=105
		If K
		Text(~1,25F+23,1," 
		F-(F and K=25)+(K=34 and F<5->F
		Text(25F+25,2,"[|>]
		getKey->K
	End
	For(X,20,130,11
		Text(~1,X,0,Str0
	End
	If F<3
	Then
		If not(F
		L->U
		If F=1
		M->U
		If F=2
		N->U
		Horizontal ~3.2,19
		If U=1
		Text(~1,30,95,"STREET
		If U=2
		Text(~1,30,100,"SPORT
		If U>=3
		Text(~1,30,105,"RACE
		"IMPROVES 
		If F<=1
		Ans+"ACCELERATION 
		If not(F
		Ans+"& TOP SPEED
		If F=2
		Ans+"GRIP
		Text(120,0,Ans+".
		"INSTALL 
		If U=1 and not(F
		Ans+"HI~FLOW AIR FILTER & EXHAUST
		If U=1 and F=1
		"REMOVE SPARE TIRE & REAR SEATS
		If U=1 and F=2
		Ans+"STREET TIRES
		If U=2 and not(F
		"REPROGRAM THE ECU
		If U=2 and F=1
		"REMOVE A/C & PASSENGER SEAT
		If U=2 and F=2
		"SOFT COMPOUND SPORTS TIRES
		If U>=3 and not(F
		Ans+"HIGH~PERFORMANCE CAMS
		If U>=3 and F=1
		"STRIP OUT INTERIOR TRIM
		If U>=3 and F=2
		Ans+"DRAG SLICKS
		Text(90,0,Ans+".
		2500U|LCAE(17->C
		Text(60,140,C
		Text(60,95,"COST:
		If U<=3
		Then
			Text(150,120,"PURCHASE
		Else
			Text(150,125,"OWNED
		End
		Repeat K>=11 and K<=15
			getKey->K
		End
		If K>=12 and K<=15
		Then
			If U<=3 and U and C<=|LCSE(1
			Then
				|LCSE(1)-C->|LCSE(1
				A+10^(F->|LCSE(5+|LCAE(1
				If not(F
				Then
					.1+|LCAE(13->|LCAE(13
					10+|LCAE(18->|LCAE(18
					10+|LCAE(19->|LCAE(19
					2+|LCAE(4->|LCAE(4
				End
				If F=1
				Then
					.1+|LCAE(13->|LCAE(13
					|LCAE(24)-50->|LCAE(24
				End
				If F=2
				|LCAE(23)-2->|LCAE(23
				TextColor(14
				8->C
				Text(~1,70,95,"OWNED
				Pause 
			End
			If C>9 and U<=3
			Then
				Text(~1,80,20,"INSUFFICIENT FUNDS
				Pause 
			End
		End
	End
	Text(~1,146,0,Str0
	If F=3
	Then
		DelVar HText(40,70,"SELL ALL UPGRADES?
		Text(70,105,"NO
		Text(95,105,"YES
		Repeat K=21 or K=105
			If K
			Text(~1,25H+68,89," 
			H-(H and K=25)+(K=34 and not(H->H
			Text(25H+70,90,"[|>]
			getKey->K
		End
		If H
		Then
			int(|LCSE(5+|LCAE(1->U
			|LCSE(1)+U-111->|LCSE(1
			~.1+fPart(.1U->A
			|LCAE(13)-A->|LCAE(13
			A10->A
			|LCAE(4)-A2->|LCAE(4
			|LCAE(18)-A10->|LCAE(18
			|LCAE(19)-A10->|LCAE(19
			|LCAE(23)-A->|LCAE(23
			~.1+fPart(.1int(.1U->A
			|LCAE(13)-A->|LCAE(13
			|LCAE(24)+500A->|LCAE(24
			|LCAE(23)+2int(sub(U->|LCAE(23
			|LCSE(5+|LCAE(1->A
			111+fPart(A->|LCSE(5+|LCAE(1
			Text(70,60,"CAR RESET.
			Pause 
		End
	End
	If F=4
	Then
		For(F,1,7
			5+18F
			Text(Ans,5,"GEAR      :
			Text(Ans,40,F
			Text(Ans,64,|LCAE(5+F
		End
		Text(149,5,"FINAL:
		Text(149,64,|LCAE(21
		Pause 
	End
End
If C=2:Then
	Horiz
	Horizontal 2.6,13
	Text(0,180,"PAINTSHOP
	Text(0,0,"CR:
	|LCSE(1->C
	Text(0,25,C
	Text(20,50,"CURRENT COLOR:
	Text(65,93,"COST: 25
	|LCAE(16
	TextColor(Ans
	Text(20,160,Ans
	If 15=|LCAE(1
	Then
		Pause "NO CAR SELECTED
		1->A
	Else
		Text(40,128,"#
		Pt-On(.2,~.7,Ans
		Pt-On(~.15,~.7,Ans
		Input "INPUT COLOR:",A
		If A>24 or A<10
		12->A
		If C>=25 and A!=|LCAE(16
		Then
			A->|LCAE(16
			sub(A)+int(|LCSE(5+|LCAE(1->|LCSE(5+|LCAE(1
			TextColor(12
			Text(20,50,Str0
			Text(20,90,"REPAINTING...
			C-25->|LCSE(1
			For(Y,43,53
				For(X,128,138)
					If 2fPart(.5(X+Y
					Pxl-On(Y,X,A
				End
			End
			TextColor(A
			Text(20,85,"COLOR CHANGED
		End
		Text(65,93,"PRESS ENTER
		128->B
		Text(40,B,"#
		Pt-On(.2,~.7,A
		Pt-On(~.15,~.7,A
		Pause 
		For(B,122,250,2
			Text(40,B,"#
		End
	End
	Full
End
If C=4:Then
	20->A
	ClrDraw
	Text(0,0,|LCSE(27
	Text(0,195,"RECORDS
	Horizontal 3.2,12
	Text(A,92,"TIME TRIALS
	Horizontal 2.3,12
	Text(40,190,"LONG
	Text(40,80,"SHORT
	Text(60,2,"TIER 1
	Text(80,2,"TIER 2
	Text(A5,2,"TIER 3
	For(F,1,3
		Text(40+AF,85,|LTTR(F
	End
	For(F,1,3
		Text(40+AF,195,|LTTR(3+F
	End
	Line(~4.4,2.2,~4.4,~1.8,12
	Text(6A,8A,"RECORD MPH:
	Text(7A,8A,|LCSE(5
	TextColor(10
	135
	Text(Ans,0,"LADDER:
	Text(Ans,55,|LCSE(2)-14
	Text(Ans,75,"/30
	TextColor(15
	Text(6A,95,iPart(|LCSE(3)-.9
	Text(6A,0,"CAR SPECIFIC:
	Text(6A,115,"/13
	TextColor(16
	150
	Text(Ans,0,"SPECIAL EVENTS:
	Text(Ans,115,|LCSE(4
	Text(Ans,130,"/6
	Horizontal 1.2,11
	Horizontal .2,14
	Horizontal ~.8,18
	Horizontal ~1.8,12
	Line(1,2.2,1,~7,12
	Pause 
End
DelVar FIf C=5:Then
	DelVar DBorderColor 3
	1->A
	Repeat F=3 or D=3
		ClrDraw
		Horizontal 3.2,17
		Horizontal 2.3,17
		Text(0,190,"BUY CARS
		Text(0,0,"CR:
		|LCSE(1->C
		Text(0,25,C
		70
		Text(20,Ans,"SELECT CONTINENT
		Text(55,Ans,"NORTH AMERICA
		Text(80,Ans,"EUROPE
		Text(105,Ans,"ASIA
		Text(130,Ans,"BACK
		Repeat K=21 or K=105
			If K
			Text(~1,25F+53,50," 
			F-(F and K=25)+(K=34 and F<3->F
			Text(25F+55,50,"[|>]
			getKey->K
		End
		For(X,20,140,15
			Text(~1,X,0,Str0
		End
		Horizontal 2.3,17
		TextColor(12
		Text(145,80,"BACK
		If not(F
		Then
			Text(20,80,"NORTH AMERICA
			Text(45,80,"CAMACO SS '69
			Text(70,80,"DMCA-12 '82
			Text(95,80,"FORKUS RS '09
			Text(120,80,"'VETTE '20
		End
		If F=1
		Then
			Text(20,90,"EUROPE
			Text(45,80,"CONTACT LP400 '74
			Text(70,80,"7-11 GT3 RS '16
			Text(95,80,"SACKS-O '97
			Text(120,80,"ENSO '03
		End
		If F=2
		Then
			Text(20,110,"ASIA
			Text(45,80,"CRUX SI '91
			Text(70,80,"SUPER-A '98
			Text(95,80,"GTR R33 '97
			Text(120,80,"GTR R35 '17
		End
		If F!=3
		Then
			Repeat K=21 or K=105
				If K
				Text(~1,25D+43,60," 
				D-(D and K=25)+(D<4 and K=34->D
				Text(25D+45,60,"[|>]
				getKey->K
			End
			If D!=4
			Then
				If not(F
				Then
					If not(D
					Then
						"CAMACO SS '69->Str3
						4|E4->C
						2->N
						2->I
						19->B
					End
					If D=1
					Then
						"DMCA-12 '82->Str3
						2|E4->C
						7->N
						1->I
						22->B
					End
					If D=2
					Then
						"FORKUS RS '09->Str3
						35|E3->C
						3->N
						2->I
						14->B
					End
					If D=3
					Then
						"'VETTE '20->Str3
						9|E4->C
						6->N
						3->I
						18->B
					End
				End
				If F=1
				Then
					If not(D
					Then
						"CONTACT LP400 '74->Str3
						11|E4->C
						4->N
						2->I
						15->B
					End
					If D=1
					Then
						"7-11 GT3 RS '16->Str3
						15|E4->C
						5->N
						3->I
						15->B
					End
					If D=2
					Then
						"SACKS-O '97->Str3
						19|E3->C
						8->N
						2->I
						12->B
					End
					If D=3
					Then
						"ENSO '03->Str3
						1|E6->C
						12->N
						3->I
						11->B
					End
				End
				If F=2
				Then
					If not(D
					Then
						"CRUX SI '91->Str3
						18|E3->C
						1->N
						1->I
						14->B
					End
					If D=1
					Then
						"SUPER-A '98->Str3
						4|E4->C
						9->N
						2->I
						20->B
					End
					If D=2
					Then
						"GTR R33 '97->Str3
						5|E4->C
						10->N
						2->I
						N->B
					End
					If D=3
					Then
						"GTR R35 '17->Str3
						115|E3->C
						11->N
						3->I
						17->B
					End
				End
				ClrDraw
				|LCSE(N+5->O
				Horizontal 3.2,17
				Text(0,190,"BUY CARS
				Horizontal ~3.2
				Text(0,0,"CR:
				Text(0,25,|LCSE(1
				If not(Z
				B->Z
				TextColor(B
				Text(50,60,Str3
				Text(75,128,"#
				Pt-On(.2,0,B
				Pt-On(~.15,0,B
				TextColor(12
				Text(25,110,"TIER
				Text(25,150,I
				Text(97,84,"COST:
				Text(120,84,"CAR #:
				Text(120,140,N
				Text(97,140,C
				Text(150,5,"BACK
				If O
				Then
					Text(150,95,"OWNED
				Else
					Text(150,95,"PURCHASE CAR
				End
				DelVar KLine(~4.5,~5,~4.5,~3.2
				Repeat K>=11 and K<=15
					getKey->K
				End
				If K>11 and not(O
				Then
					If C<=|LCSE(1
					Then
						|LCSE(1)-C->|LCSE(1
						111+sub(B->|LCSE(5+N
						TextColor(14
						Text(~1,80,50,"CAR PURCHASED
					Else
						TextColor(11
						Text(~1,80,20,"INSUFFICIENT FUNDS
					End
					TextColor(12
					Pause 
				End
			End
		End
	End
End
If C=6:Then
	ClrHome
	Disp "SAVING
	Archive |LCSE
	Archive |LTTR
	ClrDraw
	Return
End
If C=3:Then
	DelVar FClrDraw
	20
	Text(0,9Ans,"CHANGE CAR
	Horizontal 3.2
	For(Y,0,1
		For(X,0,5
			Text(AnsX+25,Ans+130Y,"???
		End
	End
	DelVar FText(145,Ans,"BACK
	If |LCSE(6
	Text(25,Ans,"CRUX Si
	If |LCSE(7
	Text(45,Ans,"CAMACO SS
	If |LCSE(8
	Text(65,Ans,"FORKUS RS
	If |LCSE(9
	Text(85,Ans,"CONTACT LP400
	If |LCSE(10
	Text(105,Ans,"7-11 GT3 RS
	If |LCSE(11
	Text(125,Ans,"'VETTE
	150
	If |LCSE(12
	Text(25,Ans,"DMCA-12
	If |LCSE(13
	Text(45,Ans,"SACKS-O
	If |LCSE(14
	Text(65,Ans,"SUPER-A
	If |LCSE(15
	Text(85,Ans,"GTR R33
	If |LCSE(16
	Text(105,Ans,"GTR R35
	If |LCSE(17
	Text(125,Ans,"ENSO
	If |LCSE(18
	Text(145,Ans,"INCRECEBA
	Repeat K=21 or K=105
		If K
		Text(~1,20A+23,4+130F," 
		A-(A and K=25)+(K=34 and A<6->A
		F-(F and K=24)+(K=26 and not(F->F
		Text(20A+25,4+130F,"[|>]
		getKey->K
	End
	26-not(A=6 and not(F->dim(|LCAE
	If F
	Then
		If A=6 and |LCSE(18
		Then
			"OXITI INCRECEBA '23->Str9
			{13,9,6,230,7,3.75,2.38,1.72,1.34,1.11,.95,.8,3.2,.25,.12,15,3,525,486,26.6,3.7,1,12,1389,3->CAE
		End
		If A=5 and |LCSE(17
		Then
			"ENSO '03->Str9
			{12,8.2,4,218,6,3.15,2.18,1.57,1.19,.94,.76,0,3.3,.2,.15,11,3,651,485,28.5,4.1,2,15,1255,1->CAE
		End
		If A=4 and |LCSE(16
		Then
			"GTR R35 '17->Str9
			{11,7.1,4.6,199,6,4.056,2.301,1.595,1.248,1.001,.796,0,3.3,.2,.16,17,3,565,467,27.9,3.7,3,9,1760,3->CAE
		End
		If A=3 and |LCSE(15
		Then
			"GTR R33 '97->Str9
			{10,8.5,8,162,5,3.214,1.92,1.302,1,.752,0,0,2.1,.2,.2,10,2,276,260,26,4.111,3,7,1530,3->CAE
		End
		If A=2 and |LCSE(14
		Then
			"SUPER-A '98->Str9
			{9,7.5,5,167,6,3.827,2.36,1.685,1.312,1,.793,0,2,.2,.16,20,2,320,315,25,3.133,3,9,1590,1->CAE
		End
		If A=1 and |LCSE(13
		Then
			"SACKS-O '97->Str9
			{8,7,3,124,5,3.333,1.955,1.286,.926,.733,0,0,1.6,.1,.22,12,1,120,107,22.7,4.056,2,6,935,2->CAE
		End
		If not(A)|LCSE(12
		Then
			"DMCA-12 '82->Str9
			{7,7.5,7,130,5,3.36,2.06,1.38,1.06,.82,0,0,1,.1,.2,21,1,130,152,26.1,3.44,1,8,1233,1->CAE
		End
	Else
		If A=5 and |LCSE(11
		Then
			"'VETTE '20->Str9
			{6,6.6,3.5,194,7,2.91,1.76,1.22,.88,.65,.51,.4,3.1,.25,.1,18,3,490,465,27.2,4.9,1,10,1654,1->CAE
		End
		If A=4 and |LCSE(10
		Then
			"7-11 GT3 RS '16->Str9
			{5,8.8,6,193,7,3.75,2.38,1.72,1.34,1.11,.96,.84,3.2,.25,.11,15,3,500,339,28.7,4.19,2,10,1420,1->CAE
		End
		If A=3 and |LCSE(9
		Then
			"CONTACT LP400 '74->Str9
			{4,8,5,186,5,2.256,1.769,1.31,.99,.775,0,0,2.2,.2,.14,15,2,375,365,25.3,4.091,2,12,1300,1->CAE
		End
		If A=2 and |LCSE(8
		Then
			"FORKUS RS '09->Str9
			{3,7,6.5,156,6,3.23,1.95,1.32,1.03,.821,.685,0,2,.25,.15,14,2,300,318,25.5,4.06,1,9,1741,2->CAE
		End
		If A=1 and |LCSE(7
		Then
			"CAMACO SS '69->Str9
			{2,6,5,138,4,2.52,1.88,1.46,1,0,0,2.59,1.4,.2,.15,19,1,299,380,25.3,3.73,1,11,1577,1->CAE
		End
		If not(A)|LCSE(6
		Then
			"CRUX SI '91->Str9
			{1,8.2,6,153,5,3.166,2.052,1.416,1.103,.87,0,0,1.2,.13,.15,11,1,108,100,22.5,4.266,3,8,954,2->CAE
		End
	End
	ClrDraw
	Text(0,180,"CHANGE CAR
	Horizontal 3.2
	Text(75,20,"GOT IN
	Text(110,99,"CAR
	Text(110,130,|LCAE(1
	Text(150,93,"PRESS ENTER
	|LCSE(5+|LCAE(1->U
	If U
	Then
		~.1+fPart(.1int(U->A
		A+|LCAE(13->|LCAE(13
		A10->A
		A2+|LCAE(4->|LCAE(4
		A10+|LCAE(18->|LCAE(18
		A10+|LCAE(19->|LCAE(19
		A+|LCAE(23->|LCAE(23
		~.1+fPart(.1int(.1U->A
		A+|LCAE(13->|LCAE(13
		|LCAE(24)-500A->|LCAE(24
		|LCAE(23)-2int(sub(U->|LCAE(23
		100fPart(U->|LCAE(16
		TextColor(Ans
	End
	Text(75,65,Str9
	Pause 
End
If C
Goto 0
Lbl 2
TextColor(12
If 15=|LCAE(1
Pause "YOU NEED A CAR TO RACE.
If 15=|LCAE(1
Goto 0
ClrHome
BackgroundOff
"CPU->Str2
1->L
"CAR SPECIFIC
If 14=|LCSE(3
"CUSTOM SPEC
DelVar DDelVar JDelVar WDelVar thetaClrDraw
Menu("        EVENT LIST"+Str0,"REGULATION",A,"LADDER",B,"TIME TRIALS",C,Ans,D,"SPECIAL EVENTS",E,"MACHINE TEST",F,"BACK TO HOME",0
Lbl F
8->L
Goto 1
Lbl E
4->W
BackgroundOn 22
DelVar CHorizontal 3.2,16
For(X,0,|LCSE(4
	TextColor(12
	If |LCSE(21+X
	TextColor(20
	If not(X
	Text(25,17,"small1. [CLASSIC] CAR CUP
	If X=1
	Text(45,17,"small2. TIER 1 CHAMPIONSHIP
	If X=2
	Text(65,17,"small3. JP TUNER TRIALS
	If X=3
	Text(85,17,"small4. TIER 2 CHAMPIONSHIP
	If X=4
	Text(105,17,"small5. TOP SPEED SHOWDOWN
	If X=5
	Text(125,17,"small6. TIER 3 CHAMPIONSHIP
End
TextColor(20
Text(0,145,"SPECIAL EVENTS
X-(X=7->X
Text(25+20X,17,"BACK
X-1->X
Repeat K=21 or K=105
	If K
	Text(~1,20C+23,4," 
	C-(C and K=25)+(K=34 and C<=|LCSE(4->C
	Text(20C+25,4,"[|>]
	getKey->K
End
|LCAE(22->B
|LCAE(17->A
11->Q
If C>X
Goto 2
If not(C
Then
	expr(sub(Str9,length(Str9)-1,2
	If 92>=Ans and 45<Ans
	Then
		"STEVE O->Str2
		1.4->J
		.4->D
	Else
		Pause "CAR MUST BE PRE-1992
		9->C
	End
End
If C=1
Then
	If A=1
	Then
		1.4->J
		19->Q
		"TJ->Str2
	Else
		Pause "TIER 1 CARS ONLY
		9->C
	End
End
If C=2
Then
	If B=3 and A=2
	Then
		"MAYEO->Str2
		.3->D
		2->J
		10->Q
	Else
		Pause "TIER 2 JP CARS ONLY
		9->C
	End
End
If C=3
Then
	If A=2
	Then
		2.1->J
		"B.D->Str2
		13->Q
	Else
		Pause "TIER 2 CARS ONLY
		9->C
	End
End
If C=4
Then
	4->L
	.5->D
	2.3->J
	14->Q
	"GAMMA->Str2
End
If C=5
Then
	If A=3
	Then
		2.9->J
		17->Q
		"BANCHOR->Str2
	Else
		Pause "TIER 3 CARS ONLY
		9->C
	End
End
If C=9
Goto 2
0->|LCSE(21+C
.1(C+1->theta
Goto 1
Lbl D
5->W
|LCSE(3->A
Text(0,165,"CAR SPECIFIC
TextColor(15
Horizontal 3.2,15
If A>=14
Then
	Horiz
	Text(~1,35,60,"CUSTOM SPEC
	Input "LAUNCH MPH?",D
	Input "ACCEL. LVL?",J
	Input "LENGTH?",L
Else
	SetUpEditor L1
	{0,9,1,12,9,1,2,3,3,0,0,3,9,1,.9,1.6,1.4,2.1,2.2,.9,1.25,1.7,1.9,2.3,2.2,2->L1
	L1(A->D
	L1(13+A->J
	1+(A=13->L
	W(A=|LCAE(1->W
	Text(~1,50,95,"CAR
	Text(~1,50,135,A
	If W
	Text(90,65,Str9
	TextColor(11
	If not(W
	Text(80,90,"REQUIRE\D
	If 111!=int(|LCSE(5+|LCAE(1
	DelVar WText(120,70,"CAR MUST BE STOCK
	Pause 
End
.1D->D
If not(W
Goto 2
Goto 1
Lbl C
2->W
Text(0,170,"TIME TRIALS
Horizontal 3.2,11
Horizontal 2.3,11
Text(20,99,"TIER
|LCAE(17
DelVar AText(20,134,Ans
Text(50,150,|LTTR(3+Ans
Text(80,150,|LTTR(Ans
Text(50,30,"LONG TRACK
Text(80,30,"SHORT TRACK
Text(110,30,"BACK
Repeat K=21 or K=105
	If K
	Text(~1,30A+51,14," 
	A-(A and K=25)+(K=34 and A<2->A
	Text(30A+50,14,"[|>]
	getKey->K
End
2-A->L
If A=2
Goto 2
|LCAE(17)+3(L=2->theta
240L/|LTTR(theta->D
"GHOST->Str2
21->Q
Goto 1
Lbl B
3->W
BorderColor 3
Text(0,170,"LADDER RACE
TextColor(18
Horizontal 3.2,18
Text(~1,80,95,|LCSE(2)-14
Text(~1,80,120,"/30
.1|LCSE(2->D
Text(40,70,"CR. PRIZE:
.5->L
Text(40,145,W500D
If 44=|LCSE(2
Text(40,145,1|E4
Text(150,90,"PRESS ENTER
Pause 
Goto 1
Lbl A
1->W
BorderColor 2
Horiz
Text(0,170,"REGULATION
TextColor(14
Horizontal 2.6,14
Input "DIFFICULTY?",D
If D<0
randInt(10,70->D
D.1->D
Input "LENGTH?",L
abs(L->L
If L>5 or not(L
1->L
Text(40,50,"CR. PRIZE:
Text(40,130,W500D/L
Pause "PRESS ENTER
Lbl 1
Full
BackgroundOff
BorderColor 1
DelVar SDelVar RDelVar PDelVar OZDecimal
0->Xmin
264->Xmax
Text(0,75,"VS.
Text(0,99,Str2
1->G
19
If W>=4 or W=2
20
TextColor(Ans
For(Y,0,250,250
	Text(~1,51,Y,Str0
	Text(~1,65,Y,Str0
	Text(~1,80,Y,Str0
	TextColor(11
End
Line(0,.3,250,.3,Ans
Line(0,.5,250,.5,Ans
Line(262,~.8,262,1.6,20
12
For(Y,14,~8,~1.5
	For(X,251,263,3)
		If 2fPart(.5(X+2fPart(Y
		Pt-On(X,.1Y+.05,Ans
	End
End
If W>=4 or W=2
Then
	Line(15,.4,35,.4,Ans
	23
End
Horizontal ~.8,Ans
Horizontal 1.65,Ans
|LCAE(16->N
TextColor(N
Text(34,0,"YOU
Text(55,0,"#
Pt-On(1,.95,Ans
Pt-On(8,.95,Ans
If W=3
|LCSE(2->rand
If W!=2 and W!=4
randInt(10,24->Q
TextColor(Q
Text(80,0,"#
Pt-On(1,~.3,Q
Pt-On(8,~.3,Q
Text(99,0,Str2
TextColor(12
If not(W
Then
	Text(34,120,"RECORD MPH:
	Text(34,202,|LCSE(5
End
Text(150,188,"LENGTH:
Text(150,243,L
Text(0,60,"PRESS 2ND TO REV
Repeat Ans=21
	getKey
End
Text(~1,0,60,Str0
Text(~1,32,0,Str0
Text(~1,99,0,Str0
|LCAE(2->A
|LCAE(3->B
|LCAE(5->C
|LCAE(14->E
|LCAE(15->H
|LCAE(21->M
|LCAE(23->V
Text(106,10,"RPM (X1000)
Text(5,98,"Time
For(Z,3,0,~.05
	TextColor(12
	Text(~1,130,25,R
	If R=round(R,0
	Text(~1,130,37,".0
	Text(~1,20,90,Z
	If (W>=4 or W=2) and not(fPart(Z
	Pt-On(18+4Z,.4,19
	getKey->K
	If R<B
	Then
		TextColor(11
		Text(150,80,"REV MORE
	Else
		If R=B
		Then
			TextColor(14
			Text(150,80,"PERFECT!
		Else
			TextColor(11
			Text(150,80,"REV LESS
		End
	End
	If K and R<A
	Then
		R+(K=21->R
		R+E2not(K=21->R
	Else
		If R>A
		A->R
		abs(R-E->R
	End
	TextColor(12
End
If W>=4 or W=2
Pt-On(18,.4,14
Text(~1,20,90,Str0
DelVar ZText(~1,130,25,R
|LCAE(13->A
If B=round(R,0
Then
	TextColor(18
	Text(150,80,"GOOD START
	5+|LCSE(1->|LCSE(1
	B.1->S
Else
	TextColor(11
	If R>B
	Then
		V+int(R/2->V
		B.1->S
	Else
		V-1->V
	End
	Text(150,80,"POOR START
	.1->S
End
If R=B
Then
	TextColor(14
	Text(150,80,"PERFECT START
	8+|LCSE(1->|LCSE(1
	.05(R->S
End
|LCAE(6->F
TextColor(12
Text(106,160,"GEAR
Text(106,115,"MPH
Text(~1,130,170,G
Repeat P>=240L
	getKey->K
	If K=25 and G<C
	Then
		G+1->G
		V-1->V
		|LCAE(5+G->F
		Text(~1,130,170,G
	End
	If K=34 and G>1
	Then
		G-1->G
		|LCAE(5+G->F
		Text(~1,130,170,G
	End
	Text(~1,130,115,round(10S,0
	round(SFM3360/|LCAE(20),0)/1|E3->R
	If R>|LCAE(2
	Then
		S-(.15A(1.1-GH->S
		TextColor(11
	End
	Text(~1,130,25,R
	If S10>|LCAE(4
	.1|LCAE(4->S
	TextColor(19
	S+(1.1-GH)(.1(A(1-.5(V>0->S
	If W>3
	D+.1J->D
	Text(~1,78,round(O/L,0)," 
	If O<240L
	Then
		O+D->O
	Else
		~9->D
	End
	round(P/L,0
	Text(~1,53,Ans," 
	If V>0
	Then
		Pt-On(Ans,1.15,12
		Pt-On(Ans,.85,12
		V-1->V
	End
	P+S->P
	TextColor(N
	round(P/L,0
	Text(55,Ans,"#
	Pt-On(Ans+1,.95,N
	Pt-On(Ans+8,.95,N
	TextColor(Q
	round(O/L,0
	Text(80,Ans,"#
	Pt-On(Ans+1,~.3,Q
	Pt-On(Ans+8,~.3,Q
	TextColor(12
	Z+1->Z
	Text(~1,20,98,Z
End
Text(0,0,"CR:
|LCSE(1->E
Text(0,20,E
If D=~9
Then
	int(L+E->|LCSE(1
	DelVar thetaText(~1,78,75,"YOU LOSE
	If W=3 and 11<=|LCSE(2
	|LCSE(2)-1->|LCSE(2
	If W=4
	0->|LCSE(21+10theta
Else
	TextColor(14
	Text(~1,53,80,"YOU WIN
	TextColor(12
	If 14>|LCSE(3
	(W=5)+|LCSE(3->|LCSE(3
	If W=3
	Then
		If 44<=|LCSE(2
		Then
			3400+|LCSE(1->|LCSE(1
			Text(95,30,"YOU'RE THE NEW LADDER CHAMP!
		Else
			1+|LCSE(2->|LCSE(2
		End
	End
	W500D
	If L>=1
	Ans/L
	int(Ans+|LCSE(1->|LCSE(1
End
TextColor(12
Text(150,80,"PRESS ENTER            
TextColor(14
Text(15,12,"+
Text(15,20,abs(E-|LCSE(1:Pause 
ClrDraw
ClrHome
10S
If not(W) and Ans>|LCSE(5
Then
	Disp "NEW SPEED RECORD!","MPH:
	Ans->|LCSE(5
	Pause Ans
End
If theta
Then
	If not(fPart(theta
	Then
		If Z<|LTTR(theta
		Then
			Disp "NEW RECORD!
			Z->|LTTR(theta
		Else
			Disp "RECORD TIED
		End
		DelVar thetaDisp "TIME:
		Pause Z
	Else
		10theta->A
		1+|LCSE(20+A->|LCSE(20+A
		Ans->B
		If fPart(A/2
		Then
			If A>|LCSE(4
			A->|LCSE(4
			Disp "EVENT
			DelVar thetaOutput(1,7,A
			Output(1,9,"COMPLETE!
			If A=5 and not(|LCSE(18
			Then
				111.11->|LCSE(18
				Disp "OXITI INCRECEBA UNLOCKED
			End
		Else
			If 3<=B
			Then
				If 6>A and A>|LCSE(4
				A->|LCSE(4
				Disp "EVENT
				DelVar thetaOutput(1,7,A
				Output(1,9,"COMPLETE!
				If A=6 and not(|LCSE(19
				Then
					111.12->|LCSE(19
					Disp "CUSTOM CAR LICENSE","OBTAINED
				End
			Else
				Output(4,11,"RACE
				Output(4,16,B+1
				If B=1
				Then
					If A=2
					Then
						.3->D
						1.5->J
						16->Q
						"MILLER->Str2
					End
					If A=4
					Then
						.2->D
						2.2->J
						12->Q
						"ROCKY->Str2
					End
					If A=6
					Then
						.3->D
						2.8->J
						24->Q
						"GIO->Str2
					End
				Else
					If A=2
					Then
						.5->D
						1.52->J
						11->Q
						"WINNACE->Str2
					End
					If A=4
					Then
						.3->D
						2.3->J
						10->Q
						"KENIDO->Str2
					End
					If A=6
					Then
						.2->D
						3->J
						"MUNCHER->Str2
						18->Q
					End
				End
				Output(7,8,"VS.
				Output(7,12,Str2
			End
		End
		Pause 
	End
End
If theta
Goto 1
Goto 0